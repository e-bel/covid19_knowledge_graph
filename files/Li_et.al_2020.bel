##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "Li_et.al_2020.bel"
SET DOCUMENT Authors = "Abish K"
SET DOCUMENT ContactInfo = "abish@causalitybiomodels.com"
SET DOCUMENT Description = "COVID Knowledge Curation project"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright Â© 2020 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0.0"

##################################################################################
# NAMESPACES Section
##################################################################################
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
DEFINE NAMESPACE GFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-gene-families/hgnc-genefamily-names-20181221.belns"
DEFINE NAMESPACE HP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20190130.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
DEFINE NAMESPACE GO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go/go-20180109.belns"
DEFINE NAMESPACE MESH AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20190708.belns"
DEFINE NAMESPACE TAX AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-taxonomy/ncbi-taxonomy-20200322.belns"
DEFINE NAMESPACE PUBCHEM AS PATTERN "^\d+$"

##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION Custom_Disease AS LIST {"COVID-19"}

# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Research"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Preprint"}
DEFINE ANNOTATION Section AS LIST {"Results"}

##################################################################################
# Statements #
##################################################################################
SET Citation = {"DOI","10.20944/preprints202003.0286.v1"}
SET PublicationType = "Research"
SET PublicationStatus = "Preprint"
SET Section = "Results"

SET Evidence = "The Newman greedy heuristic module detection algorithm was applied on the network, leading to five modules, representing the T cell receptor signalling pathway, JAK-STAT signalling pathway, C-type lectin receptor signalling pathway, Chemokine signalling pathway and Endocytosis (Figure 2C)."

SET Species = "9606"
SET Custom_Disease = "COVID-19"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") reg bp(GO:"T cell receptor signaling pathway")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") reg bp(GO:"JAK-STAT cascade")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") reg bp(GO:"inhibitory C-type lectin receptor signaling pathway")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") reg bp(GO:"stimulatory C-type lectin receptor signaling pathway")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") reg bp(GO:"chemokine-mediated signaling pathway")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") reg bp(GO:endocytosis)

SET Evidence = "Through a literature review, we identified a list of important symptoms and mechanisms linked to SARS-CoV-2, including fever, fatigue, cough 17, breathing difficulty, septic shock, viral proliferation, immunodeficiency and pulmonary fibrosis 18 (Figure 4)."

a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(DO:"COVID-19")
path(DO:"COVID-19") pos path(MESH:Fever)
path(DO:"COVID-19") pos path(MESH:Cough)
path(DO:"COVID-19") pos path(MESH:Dyspnea)
path(DO:"COVID-19") pos path(MESH:"Shock, Septic")
path(DO:"COVID-19") pos bp(MESH:"Virus Replication")
path(DO:"COVID-19") pos path(HP:Immunodeficiency)
path(DO:"COVID-19") pos path(DO:"pulmonary fibrosis")

SET Evidence = "At the time, we put forward pseudoephedrine, andrographolide, chloroquine, abacavir, baricitinib, and quercetin as repurposing candidates from our list, because there were other researches also suggesting or predicting these drugs, mainly based on our Yaozh database search and literature review."

a(CHEBI:pseudoephedrine) -| path(DO:"COVID-19")
a(CHEBI:andrographolide) -| path(DO:"COVID-19")
a(CHEBI:chloroquine) -| path(DO:"COVID-19")
a(CHEBI:abacavir) -| path(DO:"COVID-19")
a(PUBCHEM:44205240) -| path(DO:"COVID-19")
a(CHEBI:quercetin) -| path(DO:"COVID-19")

SET Evidence = "Before being treated with thalidomide + methylprednisolone, the patient showed an increase in C-reactive protein (CRP) and cytokine levels, including interleukin 6 (IL-6), interleukin 10 (IL-10) and interferon gamma (IFN-gamma) together with reduced CD4+ and CD8+ T cells counts."

path(DO:"COVID-19") pos p(HGNC:CRP)
path(DO:"COVID-19") pos p(HGNC:IL6)
path(DO:"COVID-19") pos p(HGNC:IL10)
path(DO:"COVID-19") pos p(HGNC:IFNG)
path(DO:"COVID-19") pos a(MESH:"CD4-Positive T-Lymphocytes")
path(DO:"COVID-19") pos a(MESH:"CD8-Positive T-Lymphocytes")
composite(a(CHEBI:thalidomide),a(CHEBI:"6alpha-methylprednisolone")) -| p(HGNC:CRP)
composite(a(CHEBI:thalidomide),a(CHEBI:"6alpha-methylprednisolone")) -| p(HGNC:IL10)
composite(a(CHEBI:thalidomide),a(CHEBI:"6alpha-methylprednisolone")) -| p(HGNC:IL6)
composite(a(CHEBI:thalidomide),a(CHEBI:"6alpha-methylprednisolone")) -| p(HGNC:IFNG)
composite(a(CHEBI:thalidomide),a(CHEBI:"6alpha-methylprednisolone")) -| a(MESH:"CD4-Positive T-Lymphocytes")
composite(a(CHEBI:thalidomide),a(CHEBI:"6alpha-methylprednisolone")) -| a(MESH:"CD8-Positive T-Lymphocytes")

SET Evidence = "It was previously shown that thalidomide enhances TCR-mediated T cells activation by by-passing T cell need for co-stimulation by accessory molecules, such as the B7 protein together with the CD28 protein, and therefore can overcome T cell deficiency 25 ."

a(CHEBI:thalidomide) -> act(complex(GO:"T cell receptor complex"))
act(complex(GO:"T cell receptor complex")) -> bp(GO:"T cell activation")

SET Evidence = "Finally, it was also reported that thalidomide prevents NF-kB from binding to the promoters of its target genes, including TNF-alpha and IFN-Gamma thereby reducing excessive inflammatory response 27,28 ."

a(CHEBI:thalidomide) -| complex(p(GFAM:"NF-kappa B complex subunits"),p(HGNC:TNF))
a(CHEBI:thalidomide) -| complex(p(GFAM:"NF-kappa B complex subunits"),p(HGNC:IFNG))
complex(p(GFAM:"NF-kappa B complex subunits"),p(HGNC:TNF)) -> bp(GO:"inflammatory response")
complex(p(GFAM:"NF-kappa B complex subunits"),p(HGNC:IFNG)) -> bp(GO:"inflammatory response")
UNSET Custom_Disease
UNSET Species
UNSET Section
UNSET PublicationStatus
UNSET PublicationType
UNSET Citation
