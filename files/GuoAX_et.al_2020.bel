##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "GuoAX_et.al_2020.bel"
SET DOCUMENT Authors = "Shounak Baksi"
SET DOCUMENT ContactInfo = "shounak.baksi@causalitybiomodels.com"
SET DOCUMENT Description = "COVID Knowledge Curation project"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright Â© 2020 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0.0"


##################################################################################
# NAMESPACES Section
##################################################################################

#DEFINE NAMESPACE ADO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/alzheimer-disease-ontology/alzheimer-disease-ontology-1.0.2.belns"
#DEFINE NAMESPACE BRCO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/brain-region-ontology/brain-region-ontology-1.0.0.belns"
#DEFINE NAMESPACE NIFT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/nift/NIFT.belns"

DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
#DEFINE NAMESPACE MGI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20190128.belns"
#DEFINE NAMESPACE RGD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20190128.belns"
#DEFINE NAMESPACE GFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-gene-families/hgnc-genefamily-names-20181221.belns"
#DEFINE NAMESPACE SCOMP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20190128.belns"
#DEFINE NAMESPACE SFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20190128.belns"
DEFINE NAMESPACE HP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20190130.belns"
DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20190708.belns"
#DEFINE NAMESPACE DRUGBANK AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/drugbank/drugbank-20180906.belns"
#DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-names-20181021.belns"
#DEFINE NAMESPACE PFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/pfam/pfam-names-20181024.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
#DEFINE NAMESPACE GO AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go/go-20180109.belns"
#DEFINE NAMESPACE GOBP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-biological-process/go-biological-process-20190128.belns"
#DEFINE NAMESPACE GOCC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-cellular-component/go-cellular-component-20190128.belns"
DEFINE NAMESPACE EFO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/efo/efo-20171206.belns"
DEFINE NAMESPACE MESH AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE TAX AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-taxonomy/ncbi-taxonomy-20200322.belns"
#DEFINE NAMESPACE MESHC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-chemicals/mesh-chemicals-20190128.belns"
#DEFINE NAMESPACE MESHD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20190128.belns"
#DEFINE NAMESPACE MESHPP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20190128.belns"
#DEFINE NAMESPACE MESHA AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-anatomy/mesh-anatomy-20190128.belns"
#DEFINE NAMESPACE MESHCS AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-cellular-structures/mesh-cellular-structures-20190128.belns"
#DEFINE NAMESPACE NCBIGENE AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-gene/ncbi-gene-20190224.belns"

#DEFINE NAMESPACE COVID AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-covid19/ncbi-covid19-20200326.belns"

#DEFINE NAMESPACE UNIPROT AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM AS PATTERN "^\d+$"
#DEFINE NAMESPACE dbSNP AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE FIXMECHEM AS LIST {".."}
DEFINE NAMESPACE FIXME AS PATTERN ".*"

##################################################################################
# ANNOTATIONS Section
##################################################################################
#DEFINE ANNOTATION Anatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20190128.belanno"
#DEFINE ANNOTATION CellLine AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20190128.belanno"
#DEFINE ANNOTATION Cell AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20190128.belanno"
#DEFINE ANNOTATION Disease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20190128.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20190128.belanno"
#DEFINE ANNOTATION MeSHDisease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20190128.belanno"
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
#DEFINE ANNOTATION TextLocation AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"
#DEFINE ANNOTATION BioAssay AS URL   "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/bao/bao-20190130.belanno"

#DEFINE ANNOTATION CovidAccession AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/ncbi-covid19/ncbi-accession-covid19-20200326.belanno"
DEFINE ANNOTATION Custom_Disease AS LIST {"COVID-19"}
# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Review", "Research"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Preprint", "Published"}
DEFINE ANNOTATION Section AS LIST {"Full Text", "Abstract", "Results"}

##################################################################################
# Statements #
##################################################################################
SET Citation = {"DOI", "https://www.medrxiv.org/content/10.1101/2020.04.12.20062380v1.full.pdf",  "https://doi.org/10.1101/2020.04.12.20062380"}
SET PublicationType = "Research"
SET PublicationStatus = "Preprint"
SET Section = "Results"

SET Species = "9606"
SET Custom_Disease = "COVID-19"

SET Evidence = "The presence or absence of coexisting disorders may be closely related to the survival of patients. "

path(MESH:Comorbidity) pos path(MESH:Mortality)

SET Evidence = "The most common coexisting disorder was hypertension (56.90%), followed by heart disease (38.79%), diabetes (32.76%) and cerebral infarction (14.66%). "

path(DO:hypertension) pos path(DO:"COVID-19")
path(DO:hypertension) pos path(DO:"heart disease")
path(DO:hypertension) pos path(DO:"diabetes mellitus")
path(DO:hypertension) pos path(DO:"cerebral infarction")

SET Evidence = "In addition, there were a few patients with liver diseases, bronchitis, renal diseases, tumor and obesity"

path(DO:"liver disease") pos path(DO:"COVID-19")
path(DO:"bronchitis") pos path(DO:"COVID-19")
path(DO:"kidney disease") pos path(DO:"COVID-19")
path(MESH:"Tumor Burden") pos path(DO:"COVID-19")
path(DO:obesity) pos path(DO:"COVID-19")

SET Evidence = " The most common symptom was fever (71.19%), followed by cough (55.08%), exhaustion (14.41%) and chest tightness (10.17%)."

path(HP:Fever) pos path(DO:"COVID-19")
path(HP:Cough) pos path(DO:"COVID-19")
path(HP:"Chest tightness") pos path(DO:"COVID-19")
path(FIXME:exhaustion) pos path(DO:"COVID-19") 

SET Evidence = "Besides, the clinical symptoms occupying a lower proportion should also be noted, including dyspnea, diarrhea, muscle soreness, chills, headache and palpitate and vomiting"

path(HP:Dyspnea) pos path(DO:"COVID-19")
path(DO:diarrhea) pos path(DO:"COVID-19")
path(MESH:"Muscle Weakness") pos path(DO:"COVID-19")
path(MESH:Chills) pos path(DO:"COVID-19")
path(MESH:Headache) pos path(DO:"COVID-19")
path(HP:Palpitations) pos path(DO:"COVID-19")
path(MESH:Vomiting) pos path(DO:"COVID-19")

SET Evidence = "As for clinical treatment, most patients received anti-infection therapy (65.26%), antiviral therapy (55.79%) and mechanical ventilation (47.37%)."
a(CHEBI:"antiviral agent") neg path(DO:"COVID-19")
bp(MESH:Ventilation) neg path(DO:"COVID-19")
a(FIXME:"anti-infection therapy") neg path(DO:"COVID-19")
SET Evidence = "Lower percentage of patients received traditional Chinese medicine treatment, anti-inflammatory therapy and dispelling phlegm"

a(MESH:"Medicine, Chinese Traditional") neg path(DO:"COVID-19")
a(CHEBI:"anti-inflammatory agent") neg path(DO:"COVID-19")
path(HP:phlegm) neg path(DO:"COVID-19")
SET Evidence = "Besides, extracorporeal membrane oxygenation (ECMO) and transfusion of plasma donated by recovered patients were also actively used for treatment. "
bp(MESH:"Extracorporeal Membrane Oxygenation") neg path(DO:"COVID-19")
a(FIXME:"convalescent plasma") neg path(DO:"COVID-19")

SET Evidence = "Of them, a majority of patients died of respiratory disorder including respiration failure (47.39%) and acute respiratory distress syndrome (ARDS) (5.79%), which was followed by multiple organs failure (28.93%), heart disease (14.05%) and circulatory failure (13.22%). Cardiac arrest (11.57%) and shock (10.74%) were also important causes of death"

path(DO:"respiratory failure") pos path(MESH:Mortality) 
path(EFO:"acute respiratory distress syndrome") pos path(MESH:Mortality) 
path(MESH:"Multiple Organ Failure") pos path(MESH:Mortality) 
path(DO:"heart disease") pos path(MESH:Mortality) 
path(DO:"cardiac arrest") pos path(MESH:Mortality) 
path(MESH:Shock) pos path(MESH:Mortality) 

SET Evidence = "Besides, liver injury/failure, renal injury/failure, cerebrovascular accident and disseminated intravascular coagulation (DIC) also contribute to death."
path(MESH:"Chemical and Drug Induced Liver Injury") pos path(MESH:Mortality) 
path(MESH:"Liver Failure") pos path(MESH:Mortality) 
path(DO:"end stage renal failure") pos path(MESH:Mortality)  
path(MESH:"Acute Kidney Injury") pos path(MESH:Mortality) 
SET Evidence = "As for coexisting disorders, more old patients had a history of heart disease than young patients (42.57% vs 13.33%) (P=0.03)."

bp(MESH:Aging) pos path(DO:"heart disease")

SET Evidence = "In summary, old patients had more kinds of coexisting disorders. "

bp(MESH:Aging) pos path(MESH:Comorbidity)

SET Evidence = "ACE2 and TMPRSS2 were the targets of SARS-CoV-2 in human. "

p(HGNC:ACE2) -- a(TAX:"Severe acute respiratory syndrome coronavirus 2")
p(HGNC:TMPRSS2) -- a(TAX:"Severe acute respiratory syndrome coronavirus 2")

SET Evidence = "As Figure 2 showed, TMPRSS2 was expressed highly in lung which may lead to attack of SARS-CoV-2 in lung and cause respiratory failure, ARDS, initial symptoms of cough, dyspnea, and chest tightness. "
SET MeSHAnatomy = "Lung"
p(HGNC:TMPRSS2) pos path(DO:"respiratory failure")
p(HGNC:TMPRSS2) pos path(EFO:"acute respiratory distress syndrome")
p(HGNC:TMPRSS2) pos path(HP:Cough)
p(HGNC:TMPRSS2) pos path(HP:"Chest tightness")
p(HGNC:TMPRSS2) pos path(HP:Dyspnea)
UNSET MeSHAnatomy
SET Evidence = "The high expression of TMPRSS2 in liver and kidney may contribute to injury or failure in liver and kidney."

SET MeSHAnatomy = {"Liver", "Kidney"}
p(HGNC:TMPRSS2) pos path(MESH:"Liver Failure")
p(HGNC:TMPRSS2) pos path(MESH:"Chemical and Drug Induced Liver Injury") 
p(HGNC:TMPRSS2) pos path(MESH:"Acute Kidney Injury")
p(HGNC:TMPRSS2) pos path(DO:"end stage renal failure") 
UNSET MeSHAnatomy
SET Evidence = " TMPRSS2 was also highly in stomach, which may be associated with the initial symptoms of vomiting. "

SET MeSHAnatomy = "Stomach"
p(HGNC:TMPRSS2) pos path(MESH:Vomiting)
UNSET MeSHAnatomy
SET Evidence = "While that ACE2 was highly expressed in heart may be related with the attack of SARS-CoV-2 in heart, which may lead to the initial symptoms of palpitate and chest tightness in some patients, and the direct causes of death including circulatory failure, heart disease and cardiac arrest."

SET MeSHAnatomy = "Heart"
p(HGNC:ACE2) pos path(DO:"heart disease")
p(HGNC:ACE2) pos  path(DO:"cardiac arrest")
UNSET MeSHAnatomy

SET Evidence = "ACE2 also had a high expression in adipose, which may be related with obesity."

SET MeSHAnatomy = "Adipose Tissue"
p(HGNC:ACE2) pos path(DO:obesity)
UNSET MeSHAnatomy

SET Evidence = "Besides, both TMPRSS2 and ACE2 had a high expression in transverse colon and small intestine-terminal ileum, which may be associated with the initial symptom of diarrhea. (Figure 2)"

SET MeSHAnatomy = {"Colon, Transverse", "Ileum"}
p(HGNC:ACE2) pos path(DO:diarrhea)
p(HGNC:TMPRSS2) pos path(DO:diarrhea)
UNSET MeSHAnatomy
UNSET Species
UNSET Custom_Disease
UNSET Section
UNSET PublicationStatus
UNSET PublicationType