##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "32220633.bel"
SET DOCUMENT Authors = "Priya S"
SET DOCUMENT ContactInfo = "priya@causalitybiomodels.com"
SET DOCUMENT Description = "COVID Knowledge Curation project"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright © 2020 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0.0"

##################################################################################
# NAMESPACES Section
##################################################################################
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20190708.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
DEFINE NAMESPACE GO AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go/go-20180109.belns"
DEFINE NAMESPACE MESH AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE TAX AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-taxonomy/ncbi-taxonomy-20200322.belns"
#TAX not given in template.bel
##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION MeSHDisease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20190128.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20190128.belanno"

DEFINE ANNOTATION Custom_Disease AS LIST {"COVID-19"}
# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Letter to Editor"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Preprint"}
DEFINE ANNOTATION Section AS LIST {"Full Text"}
##################################################################################
# Statements #
#################################################################################

SET Citation = {"PubMed", "Autoimmun Rev. 2020 Mar 24:102524. doi: 10.1016/j.autrev.2020.102524.", "32220633"}


SET PublicationType = "Letter to Editor"
SET PublicationStatus = "Preprint"
SET Section = "Full Text"

SET Species = "9606"

SET Evidence = "In particular, in a recent study by Qin C, et al.in which among 452 \
patients with COVID-19, most of them had increase of several \
inflammatory cytokines such as tumor necrosis factor (TNF)-α, \
interleukin (IL)-1and IL-6 [3]."

SET MeSHAnatomy = "Serum"
path(DO:"COVID-19") -> sec(p(HGNC:TNF))
path(DO:"COVID-19") -> sec(p(HGNC:IL1B))
path(DO:"COVID-19") -> sec(p(HGNC:IL6))
UNSET MeSHAnatomy

SET Evidence = "Additionally, in severe cases, a reduction of CD4+ and CD8+ T cells \
and a decrease of regulatory T cells has been found, likely due to \
high expression of proinflammatory cytokines in COVID-19 patients [3]."

SET Custom_Disease = "COVID-19"
a(MESH:Cytokines) -| a(MESH:"CD4-Positive T-Lymphocytes")
a(MESH:Cytokines) -| a(MESH:"CD8-Positive T-Lymphocytes")
a(MESH:Cytokines) -| a(MESH:"T-Lymphocytes, Regulatory")
UNSET Custom_Disease

SET Evidence = "Remarkably, in SJIA patients,the pulmonary involvement triggers a \
hyper-inflammatory reaction and occurrence of secondary \
haemophagocytic lymphohistiocytosis (sHLH), since the up-regulation of \
IFN-signature, IL-1β and IL-6 may be observed [5,8]."

SET MeSHDisease = "Arthritis, Juvenile"
a(MESH:Lung) pos path(GO:"inflammatory response")
a(MESH:Lung) pos path(DO:"hemophagocytic lymphohistiocytosis")
path(GO:"inflammatory response") pos p(HGNC:IFNG)
path(GO:"inflammatory response") pos p(HGNC:IL1B)
path(GO:"inflammatory response") pos p(HGNC:IL6)
UNSET MeSHDisease

SET Evidence = "In particular, anti-cytokine therapy, by the use of the IL-6 humanized \
monoclonal antibody, tocilizumab, successfully used for autoimmune and \
autoinflammatory diseases, is under investigation for COVID-19 \
patients with severe pneumonia [11]."

a(CHEBI:tocilizumab) neg path(MESH:"Autoimmune Diseases")
a(CHEBI:tocilizumab) neg path(MESH:"Hereditary Autoinflammatory Diseases")
SET Custom_Disease = "COVID-19"
a(CHEBI:tocilizumab) neg path(DO:"acute interstitial pneumonia")
#under investigation
UNSET Custom_Disease

SET Evidence = "A growing body of evidence showed that genetic host characteristics, \
such as IL-6 polymorphisms, may contribute to virus susceptibility, in \
specific populations and ethnicities [12], thus, in our opinion, the \
shared pathogenetic mechanisms and clinical-radiological aspects \
between hyper-inflammatory diseases and COVID-19 may suggest that \
SARS-CoV-2 could act as a triggering factor for the development of a \
rapid autoimmune and/or autoinflammatory dysregulation, leading to \
severe interstitial pneumonia, in genetic predisposed individuals."

SET Custom_Disease = "COVID-19"
p(HGNC:IL6, var("?")) pos a(TAX:"Severe acute respiratory syndrome coronavirus 2")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") pos path(MESH:"Autoimmune Diseases")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") pos path(MESH:"Hereditary Autoinflammatory Diseases")
a(TAX:"Severe acute respiratory syndrome coronavirus 2") pos path(DO:"acute interstitial pneumonia")
UNSET Custom_Disease

UNSET Species

UNSET PublicationType
UNSET PublicationStatus
UNSET Section
