##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "32166483.bel"
SET DOCUMENT Authors = "Priya S"
SET DOCUMENT ContactInfo = "priya@causalitybiomodels.com"
SET DOCUMENT Description = "COVID Knowledge Curation project"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright © 2020 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0.0"


##################################################################################
# NAMESPACES Section
##################################################################################
DEFINE NAMESPACE HP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20190130.belns"
DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20190708.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
DEFINE NAMESPACE GO AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go/go-20180109.belns"
DEFINE NAMESPACE MESH AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE TAX AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-taxonomy/ncbi-taxonomy-20200322.belns"
# TAX is not added in the template
DEFINE NAMESPACE PUBCHEM AS PATTERN "^\d+$"
##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20190128.belanno"

DEFINE ANNOTATION Assay AS LIST {"TUNEL assay"}
DEFINE ANNOTATION mode_of_administration AS LIST {"Intravenous","intramuscular","oral"}
DEFINE ANNOTATION Dose AS LIST {"0.5 g", "10 mg/kg"}
DEFINE ANNOTATION Study AS LIST  {"in vitro"}
DEFINE ANNOTATION population_type AS LIST {"Children","Adult","Russian","Chinese","Adolescents"}
# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Review"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Published"}
DEFINE ANNOTATION Section AS LIST {"Full Text"}

##################################################################################
# Statements #
##################################################################################

SET Citation = {"PubMed", "World J Pediatr. 2020 Mar 12. pii: 10.1007/s12519-020-00353-5. doi:10.1007/s12519-020-00353-5.", "32166483"}

SET PublicationType = "Review"
SET PublicationStatus = "Published"
SET Section = "Full Text"


SET Evidence = "IFN-α is a broad-spectrum antiviral drug, which could inhibit the \
synthesis of viral RNA and inhibit viral replication and spread."

SET Species = "9606"
a(CHEBI:"Interferon alfa-2a") -| a(MESH:"RNA, Viral")
a(CHEBI:"Interferon alfa-2a") -| bp(MESH:"Virus Replication")
UNSET Species

SET Evidence = "IFN-α, combined with ribavirin, which could reduce viral replication, \
moderated the host response and improved clinical outcome in MERS-CoV \
infected rhesus macaques [2]."

# species is rhesus macaques
composite(a(CHEBI:ribavirin), a(CHEBI:"Interferon alfa-2a")) -| bp(MESH:"Virus Replication")
composite(a(CHEBI:ribavirin), a(CHEBI:"Interferon alfa-2a")) -| a(MESH:"Middle East Respiratory Syndrome Coronavirus")

SET Evidence = "In the experts’ consensus statements, it could be also used for \
treatment of COVID-19 in children [9], IFN-α is the only antiviral \
drug which is clearly recommended to be used in children with \
COVID-19."

SET Species = "9606"
SET population_type = "Children"
a(CHEBI:"Interferon alfa-2a") -| path(DO:"COVID-19")
UNSET population_type
UNSET Species

SET Evidence = "Adverse reactions of IFN-α mainly include low-grade fever and flu-like \
symptoms (both in children with intramuscularly injection) [11]."

SET Species = "9606"
SET population_type = "Children"
SET mode_of_administration = "intramuscular"
a(CHEBI:"Interferon alfa-2a") pos path(HP:"Low-grade fever")
a(CHEBI:"Interferon alfa-2a") pos path(DO:influenza)
UNSET mode_of_administration
UNSET population_type
UNSET Species

SET Evidence = "Growth and development inhibition is more common when combining IFN-α \
with ribavirin. Suicidal ideation is more common in children (mainly \
adolescents) compared with adults (2.4%vs. 1%) [12]."

SET Species = "9606"
composite(a(CHEBI:ribavirin), a(CHEBI:"Interferon alfa-2a")) neg bp(MESH:"Growth and Development")
SET population_type = "Adolescents"
composite(a(CHEBI:ribavirin), a(CHEBI:"Interferon alfa-2a")) neg path(MESH:"Suicidal Ideation")
UNSET population_type
UNSET Species

SET Evidence = "Based on the clinical experiences in treating SARS [13] and MERS [14], \
LPVr is proposed to treat COVID-19."

SET Species = "9606"
SET population_type = "Children"
a(PUBCHEM:11979606) -| path(DO:"COVID-19")
UNSET population_type
UNSET Species

SET Evidence = "The most common adverse reactions of LPVr include diarrhea (adults \
19.5%; children 12%), vomiting (adults 6.8%; children 21%), rash \
(adults 5%; children 12%), etc. [19]."

SET Species = "9606"
SET population_type = {"Children","Adult"}
a(PUBCHEM:11979606) pos path(DO:diarrhea)
a(PUBCHEM:11979606) pos path(MESH:Vomiting)
a(PUBCHEM:11979606) pos path(HP:"Skin rash")
UNSET population_type
UNSET Species

SET Evidence = "LPVr may increase free bilirubin and exacerbate jaundice."

a(PUBCHEM:11979606) pos a(MESH:Bilirubin)
a(PUBCHEM:11979606) pos path(MESH:Jaundice)

SET Evidence = "Ribavirin is a broad-spectrum antiviral drug, which has inhibitory \
effects on RNA viruses and DNA viruses."

a(CHEBI:ribavirin) -| a(MESH:"RNA, Viral")
a(CHEBI:ribavirin) -| a(MESH:"DNA, Viral")

SET Evidence = "Intravenous infusion of ribavirin injections at a dose of 10 mg/kg \
every time (maximum 500 mg every time), 2–3 times daily was \
recommended for children with COVID-19 [17]."

SET Species = "9606"
SET population_type = "Children"
SET mode_of_administration = "Intravenous"
SET Dose = "10 mg/kg"
a(CHEBI:ribavirin) -| path(DO:"COVID-19")
UNSET Dose
UNSET mode_of_administration
UNSET population_type
UNSET Species

SET Evidence = "The most common adverse reactions of ribavirin in children include \
fever (80%), headache (62%), neutropenia (33%), fatigue (30%), etc. \
[20]."

SET Species = "9606"
SET population_type = "Children"
a(CHEBI:ribavirin) pos path(MESH:Fever)
a(CHEBI:ribavirin) pos path(MESH:Headache)
a(CHEBI:ribavirin) pos path(MESH:Neutropenia)
a(CHEBI:ribavirin) pos path(MESH:Fatigue)
UNSET population_type
UNSET Species

SET Evidence = "Large doses of ribavirin could cause decreased hemoglobin and serious \
heart damage."

a(CHEBI:ribavirin) pos path(HP:"Decreased hemoglobin concentration")
a(CHEBI:ribavirin) pos path(DO:"rheumatic heart disease")


SET Evidence = "CD tablets were recommended for oral administration in 18–65 years of \
infected adults at a dose of 0.5 g every time, twice daily [1], for 7 \
days."

SET Species = "9606"
SET population_type = {"Children","Adult"}
SET mode_of_administration = "oral"
SET Dose = "0.5 g"
a(PUBCHEM:64927) -| path(DO:"COVID-19")
UNSET mode_of_administration
UNSET population_type
UNSET Species

SET Evidence = "At higher doses of CD, the main adverse reaction is ocular toxicity, \
which could affect vision"

a(PUBCHEM:64927) pos path(DO:"toxic optic neuropathy")

SET Evidence = "Other adverse reactions of CD include arrhythmia, drug induced \
psychosis, leukopenia, etc."

a(PUBCHEM:64927) pos path(HP:"Arrhythmia")
a(PUBCHEM:64927) pos path(DO:"drug psychosis")
a(PUBCHEM:64927) pos path(DO:leukopenia)

SET Evidence = "CD has a direct inhibitory effect on neuromuscular junctions, which \
could be aggravated while combining with drugs (e.g., clindamycin, \
streptomycin, gentamicin, etc.)."

composite(a(PUBCHEM:64927), a(CHEBI:clindamycin)) -| bp(GO:"neuromuscular junction")
composite(a(PUBCHEM:64927), a(CHEBI:streptomycin)) -| bp(GO:"neuromuscular junction")
composite(a(PUBCHEM:64927), a(CHEBI:gentamycin)) -| bp(GO:"neuromuscular junction")

SET Evidence = "Combination of CD with heparin could increase bleeding risk."

composite(a(PUBCHEM:64927), a(CHEBI:heparin)) pos path(MESH:Hemorrhage)

SET Evidence = "Arbidol (umifenovir) is a broad-spectrum antiviral compound approved \
in Russia and China for prophylaxis and treatment of influenza."

SET Species = "9606"
SET population_type = {"Russian", "Chinese"}
a(CHEBI:umifenovir) -| path(DO:influenza)
UNSET population_type
UNSET Species

SET Evidence = "This compound shows activities against numerous DNA and RNA viruses \
[27]."

a(CHEBI:umifenovir) -| a(MESH:"RNA, Viral")
a(CHEBI:umifenovir) -| a(MESH:"DNA, Viral")

SET Evidence = "Arbidol was found to be effective to SARS-CoV-2 in vitro [28]."

SET Study = "in vitro"
a(CHEBI:umifenovir) -| a(TAX:"Severe acute respiratory syndrome coronavirus 2")
UNSET Study

SET Evidence = "Adverse reactions include nausea, diarrhea, dizziness, elevated serum \
aminotransferase, etc."

a(CHEBI:umifenovir) pos path(MESH:Nausea)
a(CHEBI:umifenovir) pos path(DO:diarrhea)
a(CHEBI:umifenovir) pos path(MESH:Dizziness)
SET MeSHAnatomy = "Serum"
a(CHEBI:umifenovir) pos a(MESH:"Aspartate Aminotransferases")
UNSET MeSHAnatomy

UNSET Section
UNSET PublicationStatus
UNSET PublicationType
